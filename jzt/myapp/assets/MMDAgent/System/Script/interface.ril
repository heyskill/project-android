;-------------------------------------------------------------------------

[include "system\script\foundation.ril"]

[const BUTTON_EVENT_FADEIN = "BUTTON_EVENT_FADEIN"]
[const BUTTON_EVENT_NEUTRAL = "BUTTON_EVENT_NEUTRAL"]
[const BUTTON_EVENT_HOVER = "BUTTON_EVENT_HOVER"]
[const BUTTON_EVENT_UNHOVER = "BUTTON_EVENT_UNHOVER"]
[const BUTTON_EVENT_PRESS = "BUTTON_EVENT_PRESS"]
[const BUTTON_EVENT_RELEASE = "BUTTON_EVENT_RELEASE"]
[const BUTTON_EVENT_DELETE = "BUTTON_EVENT_DELETE"]

[let $_button_list]
[let $_scroll_list]

;-------------------------------------------------------------------------

;-------------------------------------------------------------------------
; ボタンの有効化
;-------------------------------------------------------------------------
[macro name:"button_enable" params:"alias"]
  [let $imageAlias = "button_".$alias]
  [image_ishidden alias:$imageAlias dest:$hide]
  [if $hide][return][endif]
  
  [let $global["_button_list"][$alias]["enable"] = true]
[endmacro]
[macro name:"button_enable"]
  [foreach from:$global["_button_list"] item:$item key:$alias]
    [button_enable $alias]
  [endforeach]
[endmacro]

;-------------------------------------------------------------------------
; ボタンの無効化
;-------------------------------------------------------------------------
[macro name:"button_disable" params:"alias"]
  [let $global["_button_list"][$alias]["enable"] = false]
[endmacro]
[macro name:"button_disable"]
  [foreach from:$global["_button_list"] item:$item key:$alias]
    [button_disable $alias]
  [endforeach]
[endmacro]

;-------------------------------------------------------------------------
; ボタンの可視化
;-------------------------------------------------------------------------
[macro name:"button_show" params:"alias"]
  [image_show alias:"button_".$alias]
  [textbox_show alias:"button_".$alias."_text"]
[endmacro]
[macro name:"button_show"]
  [foreach from:$global["_button_list"] item:$item key:$alias]
    [button_show $alias]
  [endforeach]
[endmacro]

;-------------------------------------------------------------------------
; ボタンの非表示化
;-------------------------------------------------------------------------
[macro name:"button_hide" params:"alias"]
  [image_hide alias:"button_".$alias]
  [textbox_hide alias:"button_".$alias."_text"]
[endmacro]
[macro name:"button_hide"]
  [foreach from:$global["_button_list"] item:$item key:$alias]
    [button_hide $alias]
    [button_disable $alias]
  [endforeach]
[endmacro]

;-------------------------------------------------------------------------
; ボタンの有効化状態
;-------------------------------------------------------------------------
[macro name:"button_isenable" params:"alias"]
  [return $global["_button_list"][$alias]["enable"]]
[endmacro]

;-------------------------------------------------------------------------
; ボタンのフェードアウト
;-------------------------------------------------------------------------
[macro name:"button_fadeout" params:"alias"]
  [let $imageAlias = "button_".$alias]
  [let $textAlias = $imageAlias."_text"]
  [let $alpha = 1]

  [thread_delete $alias]

  [do]
    [image_color alias:$imageAlias alpha:$alpha]
    [textbox_color alias:$textAlias alpha:$alpha]
    [let $alpha -= 0.08]
    [wait DELTA_TIME]
  [dowhile 0 < $alpha]

  [image_delete alias:$imageAlias]
  [textbox_delete alias:$textAlias]
  [send_message type:BUTTON_EVENT_DELETE args:$alias]
  [unset $global["_button_list"][$alias]]
[endmacro]

;-------------------------------------------------------------------------
; ボタンの削除
;-------------------------------------------------------------------------
[macro name:"button_waitdelete"]
  [while true]
    [set $count][count $global["_button_list"]][if !$count][break][endif]
    [wait event:BUTTON_EVENT_DELETE]
  [endwhile]
[endmacro]

[macro name:"button_delete"]
  [foreach from:$global["_button_list"] item:$item key:$alias]
    [thread][button_fadeout alias:$alias][endthread]
  [endforeach]
[endmacro]

[macro name:"button_delete" params:"list"]
  [foreach from:$list item:$alias]
    [thread][button_fadeout alias:$alias][endthread]
  [endforeach]
[endmacro]

;-------------------------------------------------------------------------
; ボタンループ
;-------------------------------------------------------------------------
[macro name:"button_neutral" params:"args"]
  [let $alias = $args[0][0]]
  [let $imageAlias = "button_".$alias]
  
  [image_getposition alias:$imageAlias x:$x y:$y]
  [image_getsize alias:$imageAlias width:$width height:$height]
  [let $ishit = false]
  [let $hovering = false]

  [while true]
    [set $isenable][button_isenable $alias][if !$isenable]
      [wait DELTA_TIME]
      [continue]
    [endif]
    [set $ishit][is_mousehit x:$x y:$y width:$width height:$height]
    [if $ishit]
      ; ホバーした瞬間
      [if !$hovering]
        [let $hovering = true]
;        [sound_start alias:"hover" file:"system\sound\hover.wav"]
        [send_message type:BUTTON_EVENT_HOVER args:$alias]
      [endif]
      ; 決定
      [set $button][mouse_button MOUSE_BUTTON_LEFT][if BUTTON_PRESS == $button]
;        [sound_start alias:"selected" file:"system\sound\selected.wav"]
        [image_color alias:$imageAlias red:0.3 green:0.2 blue:0.2]
        [send_message type:BUTTON_EVENT_PRESS args:$alias]
        [break]
      [endif]
      [image_color alias:$imageAlias red:0.1 green:0.2 blue:0.2]
    [else]
      [if $hovering]
        [let $hovering = false]
        [send_message type:BUTTON_EVENT_UNHOVER args:$alias]
      [endif]
      [image_color alias:$imageAlias red:0.1 green:0.1 blue:0.1]
    [endif]
    [wait DELTA_TIME]
  [endwhile]
[endmacro]

;-------------------------------------------------------------------------
; ボタンのフェードイン
;-------------------------------------------------------------------------
[macro name:"button_fadein" params:"args"]
  [let $alias = $args[0][0]]
  [let $imageAlias = "button_".$alias]
  [let $textAlias = $imageAlias."_text"]
  [let $alpha = 0]

  [do]
    [image_color alias:$imageAlias alpha:$alpha]
    [textbox_color alias:$textAlias alpha:$alpha]
    [let $alpha += 0.08]
    [wait DELTA_TIME]
  [dowhile 1 > $alpha]

  [send_message type:BUTTON_EVENT_NEUTRAL args:$alias]
[endmacro]

;-------------------------------------------------------------------------
; ボタンのON/OFF制御
;-------------------------------------------------------------------------
[macro name:"button_switcher" params:"args"]
  [let $alias = $args[0][0]]
  [let $imageAlias = "button_".$alias]
  [image_getposition alias:$imageAlias x:$x y:$y]
  [image_getsize alias:$imageAlias width:$width height:$height]
  [let $ishit = false]

  [image_color alias:$imageAlias red:0.1 green:0.3 blue:0.2]

  [while true]
    [set $isenable][button_isenable $alias][if !$isenable]
      [wait DELTA_TIME]
      [continue]
    [endif]
    [set $ishit][is_mousehit x:$x y:$y width:$width height:$height]
    [if $ishit]
      [if !$hovering]
        [let $hovering = true]
;        [sound_start alias:"hover" file:"system\sound\hover.wav"]
        [send_message type:BUTTON_EVENT_HOVER args:$alias]
      [endif]
      ; 決定
      [set $button][mouse_button MOUSE_BUTTON_LEFT][if BUTTON_PRESS == $button]
;        [sound_start alias:"selected" file:"system\sound\selected.wav"]
        [image_color alias:$imageAlias red:0.1 green:0.1 blue:0.1]
        [send_message type:BUTTON_EVENT_RELEASE args:$alias]
        [break]
      [endif]
    [else]
      [if $hovering]
        [let $hovering = false]
        [send_message type:BUTTON_EVENT_UNHOVER args:$alias]
      [endif]
    [endif]
    [wait DELTA_TIME]
  [endwhile]
[endmacro]

;-------------------------------------------------------------------------
; ボタンの追加
;-------------------------------------------------------------------------
[macro name:"button" params:"alias, text, file, x, y, width, height, layer = 0"]
  [let $imageAlias = "button_".$alias]
  [let $textAlias = $imageAlias."_text"]

  [button_enable $alias]

  [image alias:$imageAlias file:$file layer:$layer]
  [image_clip alias:$imageAlias x:0 y:0 width:$width height:$height]
  [image_move alias:$imageAlias x:$x y:$y absolute:true]
  [image_color alias:$imageAlias red:0.1 green:0.1 blue:0.1 alpha:0]

  [textbox alias:$textAlias fontsize:14 layer:$layer][ch $text][endtextbox]
  [textbox_getsize alias:$textAlias width:$textWidth height:$textHeight]
  [textbox_move alias:$textAlias x:$x+($width-$textWidth)/2.0 y:$y+($height-$textHeight)/2.0 absolute:true]
  [textbox_color alias:$textAlias alpha:0]
  
  [image_show alias:$imageAlias]
  [textbox_show alias:$textAlias]

  ; フェードイン
  [send_message type:BUTTON_EVENT_FADEIN args:$alias]
[endmacro]

;-------------------------------------------------------------------------
; 小さいボタンの追加
;-------------------------------------------------------------------------
[macro name:"button_small" params:"alias, text, x, y, layer = 0"]
  [button alias:$alias text:$text file:"system\image\button01.png" x:$x y:$y width:100 height:25 layer:$layer]
[endmacro]

;-------------------------------------------------------------------------
; 横長ボタンの追加
;-------------------------------------------------------------------------
[macro name:"button_long" params:"alias, text, x, y, layer = 0"]
  [button alias:$alias text:$text file:"system\image\button00.png" x:$x y:$y width:320 height:25 layer:$layer]
[endmacro]

;-------------------------------------------------------------------------
; ボタンイベント
;-------------------------------------------------------------------------
[macro name:"button_listen" params:"alias, call, event"]
  [listen alias:$alias call:$call event:$event args:$alias wait:false]
[endmacro]

;-------------------------------------------------------------------------
; ロード画面の表示
;-------------------------------------------------------------------------
[macro name:"load_start"]
  [image alias:"loading" file:"system\image\nowloading.png" layer:100]
  [image_color alias:"loading" alpha:0]
  [image_show alias:"loading"]
  [let $alpha = 0]
  [while 1 > $alpha]
    [image_color alias:"loading" alpha:$alpha]
    [let $alpha += 0.02]
    [wait DELTA_TIME]
  [endwhile]
[endmacro]

;-------------------------------------------------------------------------
; ロード完了
;-------------------------------------------------------------------------
[macro name:"_load_end"]
  [let $alpha = 1]
  [while 0 < $alpha]
    [image_color alias:"loading" alpha:$alpha]
    [let $alpha -= 0.02]
    [wait DELTA_TIME]
  [endwhile]
  [image_delete alias:"loading"]
[endmacro]
[macro name:"load_end"]
  [thread][_load_end][endthread]
[endmacro]

;-------------------------------------------------------------------------
; スクロールループ
;-------------------------------------------------------------------------
[macro name:"scrollbox_main" params:"alias"]
  [while true]
    [wait DELTA_TIME]
  [endwhile]
[endmacro]

;-------------------------------------------------------------------------
; スクロールボックスの追加
;-------------------------------------------------------------------------
[macro name:"scrollbox" params:"x, y, width, height"]
  [set $alias][count $global["_scroll_list"]]
  [let $global["_scroll_list"][$alias]["x"] = $x]
  [let $global["_scroll_list"][$alias]["y"] = $y]
  [let $global["_scroll_list"][$alias]["width"] = $width]
  [let $global["_scroll_list"][$alias]["height"] = $height]
  [thread][scrollbox_main $alias][endthread]
  [return $alias]
[endmacro]

;-------------------------------------------------------------------------
; メニューボタンによる表示ON/OFFの切り替え
;-------------------------------------------------------------------------
[let $_button_menu_hide = false]
[macro name:"menu_click"]
  [if $global["_button_menu_hide"]]
    [image_show]
    [textbox_show]
    [button_show]
    [button_enable]
  [else]
    [image_hide]
    [textbox_hide]
    [button_hide]
  [endif]
  [let $global["_button_menu_hide"] = !$global["_button_menu_hide"]]
[endmacro]
[listen call:"menu_click" event:"KEY" args:"C"]


