[include "System\Script\interface.ril"]

;-----

[let $posx = 10]
[let $posy = 20]

;-------------------------------------------------------------------------
; クリア
;-------------------------------------------------------------------------
[macro name:"menu_clear_press" params:"args"]
  [unset $global["share"]["dance"]]
  [textbox alias:"music_file"]未選択[endtextbox]
  [textbox alias:"motion_file"]未選択[endtextbox]
  [textbox alias:"camera_file"]未選択[endtextbox]
  [button_neutral $args]
[endmacro]

[let $alias = "clear"]
[button_small alias:$alias text:"クリア" x:$posx y:$posy layer:1000]
[button_listen call:"menu_clear_press" event:BUTTON_EVENT_PRESS alias:$alias]
[button_listen call:"button_fadein" event:BUTTON_EVENT_FADEIN alias:$alias]
[button_listen call:"button_neutral" event:BUTTON_EVENT_NEUTRAL alias:$alias]
[let $posy += 30]

;-------------------------------------------------------------------------
; 音楽
;-------------------------------------------------------------------------
[macro name:"music_file_selected" params:"args"]
  [set $length][strlen $args[1][0]]
  [if 0 == $length][return][endif]
  
  [let $global["share"]["dance"]["music"] = $args[1][0]]
  [textbox alias:"music_file"][ch $global["share"]["dance"]["music"]][endtextbox]
[endmacro]
[listen call:"music_file_selected" event:"FILE_EVENT_SELECT" args:"MUSIC"]

[macro name:"menu_music_press" params:"args"]
  [file_select alias:"MUSIC" dir:"Dance" ext:"wav; mp3; ogg"]
  [button_neutral $args]
[endmacro]

[let $alias = "music_change"]
[button_long alias:$alias text:"音楽" x:$posx y:$posy layer:1000]
[button_listen call:"menu_music_press" event:BUTTON_EVENT_PRESS alias:$alias]
[button_listen call:"button_fadein" event:BUTTON_EVENT_FADEIN alias:$alias]
[button_listen call:"button_neutral" event:BUTTON_EVENT_NEUTRAL alias:$alias]
[let $posy += 30]

; 選択内容
[textbox alias:"music_file" layer:999]
  [set $isnull][isnull $share["dance"]["music"]][if $isnull]
    未選択
  [else]
    [ch $share["dance"]["music"]]
  [endif]
[endtextbox]
[textbox_move alias:"music_file" absolute:true x:$posx + 20 y:$posy]
[textbox_show alias:"music_file"]
[let $posy += 30]

;-------------------------------------------------------------------------
; カメラモーション
;-------------------------------------------------------------------------
[macro name:"camera_file_selected" params:"args"]
  [set $length][strlen $args[1][0]]
  [if 0 == $length][return][endif]
  
  [let $global["share"]["dance"]["camera"] = $args[1][0]]
  [textbox alias:"camera_file"][ch $global["share"]["dance"]["camera"]][endtextbox]
[endmacro]
[listen call:"camera_file_selected" event:"FILE_EVENT_SELECT" args:"CAMERA"]

[macro name:"menu_camera_press" params:"args"]
  [file_select alias:"CAMERA" dir:"Dance" ext:"vmd"]
  [button_neutral $args]
[endmacro]

[let $alias = "camera_change"]
[button_long alias:$alias text:"カメラモーション" x:$posx y:$posy layer:1000]
[button_listen call:"menu_camera_press" event:BUTTON_EVENT_PRESS alias:$alias]
[button_listen call:"button_fadein" event:BUTTON_EVENT_FADEIN alias:$alias]
[button_listen call:"button_neutral" event:BUTTON_EVENT_NEUTRAL alias:$alias]
[let $posy += 30]

; 選択内容
[let $alias = "camera_file"]
[textbox alias:$alias layer:999]
  [set $isnull][isnull $share["dance"]["camera"]][if $isnull]
    未選択
  [else]
    [ch $share["dance"]["camera"]]
  [endif]
[endtextbox]
[textbox_move alias:"camera_file" absolute:true x:$posx + 20 y:$posy]
[textbox_show alias:"camera_file"]
[let $posy += 30]

;-------------------------------------------------------------------------
; モデルモーション
;-------------------------------------------------------------------------
[macro name:"motion_file_selected" params:"args"]
  [set $id][substr src:$args[0][0] offset:7]
  [let $file = $args[1][0]]

  [set $length][strlen $file]
  [if 0 == $length][return][endif]
  
  [let &$actor = $global["share"]["actors"][$id]]
  
  [let $actor["dance"] = $file]
  [textbox alias:"motion_file_".$id][ch $actor["dance"]][endtextbox]
[endmacro]

[macro name:"menu_motion_press" params:"args"]
  [file_select alias:"MOTION_".$args[0][0] dir:"Dance" ext:"vmd"]
  [button_neutral $args]
[endmacro]

[foreach from:$share["actors"] item:$actor key:$id]
  [model_getinfo alias:"actor_".$id dest:$model_info]
  
  [let $alias = $id]
  [button_long alias:$alias text:$model_info["name"] x:$posx y:$posy layer:1000]
  [button_listen call:"menu_motion_press" event:BUTTON_EVENT_PRESS alias:$alias]
  [button_listen call:"button_fadein" event:BUTTON_EVENT_FADEIN alias:$alias]
  [button_listen call:"button_neutral" event:BUTTON_EVENT_NEUTRAL alias:$alias]
  [listen call:"motion_file_selected" event:"FILE_EVENT_SELECT" args:"MOTION_".$id]
  [let $posy += 30]
  
  ; ファイル
  [let $alias = "motion_file_".$id]
  [textbox alias:$alias layer:999]
    [set $isnull][isnull $actor["dance"]][if $isnull]
      未選択
    [else]
      [ch $actor["dance"]]
    [endif]
  [endtextbox]
  [textbox_move alias:$alias absolute:true x:$posx + 20 y:$posy]
  [textbox_show alias:$alias]
  [let $posy += 30]
[endforeach]

;-------------------------------------------------------------------------
; ポーズモード
;-------------------------------------------------------------------------
[let $posy += 30]
[let $caption_posy += 30]

[macro name:"scene_pose"]
  [textbox_delete]
  [button_delete]
  [button_waitdelete]
  [script_add file:"System\Script\scenePose.ril"]
  [kill]
[endmacro]

[macro name:"close_press" params:"args"]
  [thread][scene_pose][endthread]
[endmacro]

[let $alias = "pose_mode"]
[button_small alias:$alias text:"ポーズモード" x:20 y:SCREEN_HEIGHT - 50 layer:1000]
[button_listen call:"close_press" event:BUTTON_EVENT_PRESS alias:$alias]
[button_listen call:"button_fadein" event:BUTTON_EVENT_FADEIN alias:$alias]
[button_listen call:"button_neutral" event:BUTTON_EVENT_NEUTRAL alias:$alias]

;-------------------------------------------------------------------------
; 警告表示
;-------------------------------------------------------------------------
[macro name:"assert" params:"message"]
  [textbox alias:"assert" layer:1000][ch $message][endtextbox]
  [textbox_getsize alias:"assert" width:$width height:$height]
  [textbox_move alias:"assert" x:(SCREEN_WIDTH-$width)/2 y:SCREEN_HEIGHT - 100 absolute:true]
  [textbox_color alias:"assert" alpha:0]
  [textbox_show alias:"assert"]
  [while 1 > $alpha]
    [textbox_color alias:"assert" alpha:$alpha]
    [let $alpha += 0.01]
    [wait 0.001]
  [endwhile]
  [wait 2]
  [while 0 < $alpha]
    [textbox_color alias:"assert" alpha:$alpha]
    [let $alpha -= 0.01]
    [wait 0.001]
  [endwhile]
  [textbox_hide alias:"assert"]
[endmacro]

;-------------------------------------------------------------------------
; スタート
;-------------------------------------------------------------------------
[macro name:"scene_play"]
  [button_delete]
  [button_waitdelete]
  [textbox_delete]
  [script_add file:"System\Script\sceneDancePlay.ril"]
  [kill]
[endmacro]

[macro name:"start_press" params:"args"]
  [let $ok = false]
  [foreach from:$global["share"]["actors"] item:$actor key:$id]
    [set $isnull][isnull $actor["dance"]]
    [if !$isnull]
      [let $ok = true]
      [break]
    [endif]
  [endforeach]
  
  [if !$ok]
    [assert "最低一人モーションを選択してください"][thread][button_neutral $args][endthread][return]
  [endif]
  
  [thread][scene_play][endthread]
[endmacro]

[let $alias = "start"]
[button_small alias:$alias text:"スタート" x:SCREEN_WIDTH - 120 y:SCREEN_HEIGHT - 50 layer:1000]
[button_listen call:"start_press" event:BUTTON_EVENT_PRESS alias:$alias]
[button_listen call:"button_fadein" event:BUTTON_EVENT_FADEIN alias:$alias]
[button_listen call:"button_neutral" event:BUTTON_EVENT_NEUTRAL alias:$alias]
