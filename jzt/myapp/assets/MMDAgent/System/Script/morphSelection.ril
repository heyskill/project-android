[include "system\script\interface.ril"]

;-----

; 設定ファイルを読み込む
[macro name:"load"]
  [let $x = 20]
  [let $y = 200]
  [let $count = 0]
  [let $model_alias = "actor_".$global["share"]["actor_id"]]
  [morph_getlist model:$model_alias dest:$morphNameList]
  [foreach from:$morphNameList item:$morphName]
    [let $alias = $morphName]
    [button alias:$alias text:$morphName file:"system\image\button01.png" x:$x y:$y width:100 height:25 layer:1000]
    [button_listen call:"button_press" event:BUTTON_EVENT_PRESS alias:$alias]
    [button_listen call:"button_release" event:BUTTON_EVENT_RELEASE alias:$alias]
    [button_listen call:"button_fadein" event:BUTTON_EVENT_FADEIN alias:$alias]
    [button_listen call:"load_setting" event:BUTTON_EVENT_NEUTRAL alias:$alias]
    
    [let $count++]
    [if 10 == $count]
      [let $count = 0]
      [let $x += 100]
      [let $y = 200]
    [else]
      [let $y += 34]
    [endif]
  [endforeach]
[endmacro]

; モデルが変更された
[macro name:"change"]
  [button_delete][button_waitdelete]
  [load]
[endmacro]
[macro name:"model_change" params:"args"]
  [thread][change][endthread]
[endmacro]
[listen call:"model_change" event:"MODEL_CHANGE"]

; 閉じる
[macro name:"release" params:"args"]
  [button_delete][button_waitdelete]
  [kill]
[endmacro]
[listen call:"release" event:"MORPH_CLOSE"]

; 隠す
[macro name:"hide" params:"args"]
  [button_hide]
[endmacro]
[listen call:"hide" event:"MORPH_HIDE"]

; 表示する
[macro name:"show" params:"args"]
  [button_show]
  [button_enable]
[endmacro]
[listen call:"show" event:"MORPH_SHOW"]

; ON
[macro name:"button_press" params:"args"]
  [let $morphName = $args[0][0]]
  [let $weight = 1]
  [let $id = $global["share"]["actor_id"]]
  [let $model_alias = "actor_".$id]
  [morph model:$model_alias name:$morphName weight:$weight]
  [wait event:"MOTION_EVENT_DELETE" args:$model_alias."|".$morphName]
  [let $global["share"]["actors"][$id]["morphs"][$morphName] = $weight]
  [button_switcher $args]
[endmacro]

; OFF
[macro name:"button_release" params:"args"]
  [let $morphName = $args[0][0]]
  [let $weight = 0]
  [let $id = $global["share"]["actor_id"]]
  [let $model_alias = "actor_".$id]
  [morph model:$model_alias name:$morphName weight:$weight]
  [wait event:"MOTION_EVENT_DELETE" args:$model_alias."|".$morphName]
  [let $global["share"]["actors"][$id]["morphs"][$morphName] = $weight]
  [button_neutral $args]
[endmacro]

; 状態の読み込み
[macro name:"load_setting" params:"args"]
  [let $morphName = $args[0][0]]
  [morph_getinfo model:"actor_".$global["share"]["actor_id"] name:$morphName dest:$morph]
  [if 0 == $morph["weight"]]
    [button_neutral $args]
  [else]
    [button_switcher $args]
  [endif]
[endmacro]

[load]
